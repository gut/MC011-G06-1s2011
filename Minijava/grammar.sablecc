Package minijava;

Helpers
	upper_case = ['A'..'Z'];
	lower_case = ['a'..'z'];
	letter = upper_case | lower_case;
	digit = ['0'..'9'];

	cr = 13;
	lf = 10;
	eol = cr | lf | cr lf;

	unicode_input_character = [0..0xffff];
	input_character = [unicode_input_character - [cr + lf]];
	not_star =    [input_character - '*'] | eol;
	not_star_not_slash = [input_character - ['*' + '/']] | eol;

Tokens
    /* definitions */
    tok_class = 'class';
    tok_public = 'public';  /* also used for method definition */
    tok_static = 'static';
    tok_main = 'main';
    tok_extends = 'extends';

    /* operators */
    and = '&&';
    eq = '==';
    lt = '<';
    minus = '-';
    plus = '+';
    times = '*';  /* also used as a pointer */

    /* types */
    tok_boolean = 'boolean';
    tok_int = 'int';
	tok_string = 'String';
    tok_void = 'void';

    /* flow control */
	tok_if = 'if';
	tok_else = 'else';
	tok_while = 'while';
    tok_return = 'return';

    /* other tokens */
    tok_print = 'System.out.println';
    tok_length = 'length';
    tok_true = 'true';
    tok_false = 'false';
    tok_this = 'this';
    tok_new = 'new';
    tok_null = 'null';

    id = letter (letter | digit | '_')*;
    integer_literal = digit+;
    l_brack = '[';
    l_brace = '{';
    l_paren = '(';
    r_brack = ']';
    r_brace = '}';
    r_paren = ')';
    semi  = ';';
    comma = ',';
    period = '.';
    assign = '=';
    not = '!';

    /* ignores tab and all end of line chars */
	white_space = ' ' | eol | 9;

	block_comment = '/*' not_star+ '*'+ (not_star_not_slash not_star* '*'+)* '/';
	line_comment = '//' input_character* eol?;

Ignored Tokens
	block_comment, line_comment, white_space;

Productions
	prog {-> program} = main_class class_decl*
		{-> New program(main_class.main_class, [class_decl.class_decl])};

	main_class {-> main_class} = 
		tok_class [name]:id 
		[class_l_brace]:l_brace 
			tok_public tok_static tok_void tok_main 
					l_paren tok_string l_brack r_brack [args]:id r_paren
				statement
		[class_r_brace]:r_brace 
			{-> New main_class(name, args, statement.statement)} ;

	class_decl {-> class_decl} =
		{simple_class} tok_class [name]:id
		l_brace 
			var_decl*
			method_decl*
		r_brace 
			{-> New class_decl(
				name, Null, [var_decl.var_decl], [method_decl.method_decl]
			)} | 

		{ext_class} tok_class [name]:id tok_extends [super]:id
		l_brace 
			var_decl*
			method_decl*
		r_brace
			{-> New class_decl(
				name, super, [var_decl.var_decl], [method_decl.method_decl]
			)};

	var_decl {-> var_decl} = type id semi
		{-> New var_decl(type.type, id)};

	method_decl {-> method_decl} = 
		tok_public type [name]:id l_paren arg_list? r_paren
		l_brace
			var_decl*
			statement*
			tok_return exp semi
		r_brace
			{-> New method_decl(
				type.type, name, [arg_list.formal], [var_decl.var_decl],
				[statement.statement], exp.exp
			)};

	arg_list {-> formal} = {arg_single} type id arg_rest*
        {-> New formal(type.type, id)};

	arg_rest {-> formal} = {arg_rest} comma type id
		{-> New formal(type.type, id)};

	type {-> type} = 
		{array} tok_int l_brack r_brack 
			{-> New type.array(tok_int)} |
		{int} tok_int
			{-> New type.int(tok_int)} |
		{boolean} tok_boolean
			{-> New type.boolean(tok_boolean)} |
		{object} id
			{-> New type.object(id)};

	/* treating ifs */
	/*
	 *always bind the ELSE to the last IF defined, so
	 *    if (X)  if (Y) S1  else S2
	 * is the same as:
	 *    if (X) {if (Y) S1  else S2}
	 * and NOT the same as:
	 *    if (X) {if (Y) S1} else S2
	 */
	statement {-> statement} = 
		{block} l_brace statement+ r_brace
			{-> New statement.block(l_brace, [statement.statement])} | 
		{while} tok_while l_paren exp r_paren statement
			{-> New statement.while(exp.exp, statement.statement)} | 
		{print} tok_print l_paren exp? r_paren semi
			{-> New statement.print(tok_print, exp.exp)} | 
		{assign} id assign exp semi
			{-> New statement.assign(id, exp.exp)} | 
		{array_assign} id l_brack [index]:exp r_brack assign [value]:exp semi
			{-> New statement.array_assign(id, index.exp, value.exp)} |
		{if_only} tok_if l_paren exp r_paren statement
			{-> New statement.if(exp.exp, statement.statement, Null)} |
		{if_else} tok_if l_paren exp r_paren [true]:statement_no_short_if tok_else [false]:statement
			{-> New statement.if(exp.exp, true.statement, false.statement)};

	statement_no_short_if {-> statement} =
		{block} l_brace statement+ r_brace
			{-> New statement.block(l_brace, [statement.statement])} | 
		{while} tok_while l_paren exp r_paren statement_no_short_if
			{-> New statement.while(exp.exp, statement_no_short_if.statement)} | 
		{print} tok_print l_paren exp? r_paren semi
			{-> New statement.print(tok_print, exp.exp)} | 
		{assign} id assign exp semi
			{-> New statement.assign(id, exp.exp)} | 
		{array_assign} id l_brack [index]:exp r_brack assign [value]:exp semi
			{-> New statement.array_assign(id, index.exp, value.exp)} |
		{if_else} tok_if l_paren exp r_paren [true]:statement_no_short_if tok_else [false]:statement_no_short_if
			{-> New statement.if(exp.exp, true.statement, false.statement)};

	exp {-> exp} =
		{void} exp_void
			{-> exp_void.exp} |
		{new} exp_new
			{-> exp_new.exp} |
		{cmp} exp_cmp
			{-> exp_cmp.exp};

	exp_void {-> exp} =
		{new_array} tok_new tok_int l_brack exp_method r_brack
			{-> New exp.new_array(l_brack, exp_method.exp)};

	exp_new {-> exp} =
		{new_object} tok_new id l_paren r_paren
			{-> New exp.new_object(id)};

	exp_cmp {-> exp} = 
		{and} [left]:exp_cmp and [right]:inequation_term
			{-> New exp.and(and, left.exp, right.exp)} |
		{inequation_term} inequation_term
			{-> inequation_term.exp};

	inequation_term {-> exp} =
		{equal} [left]:inequation_term eq [right]:plus_term
			{-> New exp.equal(left.exp, right.exp)} |
		{less_than} [left]:inequation_term lt [right]:plus_term
			{-> New exp.less_than(left.exp, right.exp)} |
		{plus_term} plus_term
			{-> plus_term.exp};

	plus_term {-> exp} =
		{plus} [left]:plus_term plus [right]:times_term 
			{-> New exp.plus(plus, left.exp, right.exp)} |
		{minus} [left]:plus_term minus [right]:times_term
			{-> New exp.minus(minus, left.exp, right.exp)} |
		{times_term} times_term 
			{-> times_term.exp};

	times_term {-> exp} = 
		{times} [left]:times_term times [right]:not_term
			{-> New exp.times(left.exp, right.exp)} |
		{not_term} not_term
			{-> not_term.exp};

	not_term {-> exp} = 
		{not} not not_term
			{-> New exp.not(not_term.exp)} |
		{exp_value} exp_value
			{-> exp_value.exp};

	exp_value {-> exp} = 
		{array_lookup} [id]:exp_value l_brack [index]:exp_method r_brack
			{-> New exp.array_lookup(l_brack, id.exp, index.exp)} |
		{exp_method} exp_method
			{-> exp_method.exp};

	exp_method {-> exp} =
		/* Exp Method */
		{array_length} exp_method period tok_length
			{-> New exp.array_length(exp_method.exp)} |
		{call}	[obj]:exp_method period id l_paren [args]:exp_list? r_paren
			{-> New exp.call(obj.exp, id, [args.exp])} |
		/* Exp New */
		{length_void} exp_void period tok_length
			{-> New exp.array_length(exp_void.exp)} |
		{call_new}	[obj]:exp_new period id l_paren [args]:exp_list? r_paren
			{-> New exp.call(obj.exp, id, [args.exp])} |
		{factor} factor
			{-> factor.exp};

	factor {-> exp} = 
		{identifier} id
			{-> New exp.identifier(id)} |
		{this} tok_this
			{-> New exp.this(tok_this)} |
		{true} tok_true
			{-> New exp.true(tok_true)} |
		{false} tok_false
			{-> New exp.false(tok_false)} |
		{integer_literal} integer_literal
			{-> New exp.integer_literal(integer_literal)} |
		{exp_par} l_paren exp r_paren
			{-> exp.exp};

	exp_list {-> exp} = {exp_single} exp exp_rest*
		{-> exp.exp};

	exp_rest {-> exp} = {exp_rest} comma exp
		{-> exp.exp};
	
Abstract Syntax Tree
	program = main_class [classes]:class_decl*;
	main_class = [name]:id [main_args]:id statement;
	class_decl = [name]:id [super]:id? [attributes]:var_decl* [methods]:method_decl*;
	var_decl = type [name]:id;
	method_decl = [return_type]:type [name]:id [formals]:formal* [locals]:var_decl* [body]:statement* [return_exp]:exp;
	type = {array} tok_int |
        {int} [token]:tok_int |
        {boolean} tok_boolean |
		{object} id;
	statement = {block} [token]:l_brace [statements]:statement+ |
		{if} exp [true]:statement [false]:statement? |
		{while} exp statement |
		{print} [token]:tok_print [value]:exp? |
		{assign} id exp |
		{array_assign} id [index]:exp [value]:exp;
	exp = {plus} [token]:plus [lhs]:exp [rhs]:exp |
		{minus} [token]:minus [lhs]:exp [rhs]:exp |
		{array_lookup} [token]:l_brack [array]:exp [index]:exp |
		{array_length} [array]:exp |
		{call} [object]:exp [method]:id [actuals]:exp* |
		{new_array} [token]:l_brack [size]:exp |
		{new_object} [name]:id |
		{times} [lhs]:exp [rhs]:exp |
		{and} [token]:and [lhs]:exp [rhs]:exp |
		{less_than} [lhs]:exp [rhs]:exp |
		{equal} [lhs]:exp [rhs]:exp |
		{not} [value]:exp |
		{identifier} [name]:id |
		{this} [token]:tok_this |
		{true} [token]:tok_true |
		{false} [token]:tok_false |
		{integer_literal} [value]:integer_literal;
    formal = type id;

/*vim:sw=2:ts=2:et*/
